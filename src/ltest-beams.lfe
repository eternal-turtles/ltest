(defmodule ltest-beams
  (export
   (app-ebin-dir 2)
   (paths 0) (paths 1) (paths 2) (paths 3)))

(defun app-ebin-dir (profile app)
  (filename:join (list (ltest-const:defualt-built-dir)
                       (atom_to_list profile)
                       "lib"
                       app
                       "ebin"
                       "*.beam")))

(defun paths ()
  (paths (ltest-util:app-name)))

(defun paths
  ((app) (when (is_atom app))
   (paths (list app)))
  ((apps) (when (is_list apps))
   (paths (ltest-const:default-test-profile) apps)))

(defun paths
  ((profile apps) (when (is_atom profile))
   (paths (list profile) apps))
  ((profiles apps) (when (is_list profiles))
   (lists:append
    (lists:map #'filelib:wildcard/1
               (list-comp ((<- profile profiles)
                           (<- app apps))
                      		(app-ebin-dir profile app))))))

(defun paths
  ;; "Get the compiled .beam files, but without the .beam extension. The list of
  ;; files generated by this function are meant to be consumed by (code:load_abs)."
  ((profile apps 'no-extension)
   (lists:map
     #'filename:rootname/1
     (paths profile apps))))
